(define (scan var vars vals)
  (cond ((null? vars) '())
        ((eq? var (car vars)) vals)
        (else
          (scan var (cdr vars) (cdr vals)))))

(define (lookup-variable-value var env)
  (define (env-loop env)
    (if (eq? env the-empty-environment)
      (error "Unbound variable" var)
      (let ((frame (first-frame env)))
        (let ((result-of-scan (scan var (frame-variables frame) (frame-values frame))))
          (if (null? result-of-scan)
            (env-loop (enclosing-environment env))
            (car result-of-scan))))))
  (env-loop env))

(define (set-variable-value! var val env)
  (define (env-loop env)
    (if (eq? env the-empty-environment)
      (error "Unbound variable -- SET!" var)
      (let ((frame (first-frame env)))
        (let ((result-of-scan (scan var (frame-variables frame) (frame-values frame))))
          (if (null? result-of-scan)
            (env-loop (enclosing-environment env))
            (set-car! result-of-scan val))))))
  (env-loop env))

(define (define-variable! var val env)
  (let ((frame (first-frame env)))
    (let ((result-of-scan (scan var (frame-variables frame) (frame-values frame))))
      (if (null? result-of-scan)
        (add-binding-to-frame! var val frame)
        (set-car! result-of-scan val)))))
